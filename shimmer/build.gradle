plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.23'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'dev.abdus.apps.shimmer'
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        applicationId "dev.abdus.apps.shimmer"
        minSdkVersion 23
        targetSdkVersion libs.versions.targetSdk.get()
        versionCode project.hasProperty('version.code') ? project.property('version.code').toInteger() : 1
        versionName project.hasProperty('version.name') ? project.property('version.name') : '1.0.0'
    }

    // Load keystore properties from keystore.properties file if it exists
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    // Priority: environment variables > keystore.properties > project properties
    def keystorePath = System.getenv("KEYSTORE_FILE") ?: keystoreProperties['storeFile'] ?: project.findProperty("KEYSTORE_FILE")

    signingConfigs {
        if (keystorePath) {
            release {
                storeFile file(keystorePath)
                storePassword System.getenv("KEYSTORE_PASSWORD") ?: keystoreProperties['storePassword'] ?: project.findProperty("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS") ?: keystoreProperties['keyAlias'] ?: project.findProperty("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD") ?: keystoreProperties['keyPassword'] ?: project.findProperty("KEY_PASSWORD")
            }
            debug {
                storeFile file(keystorePath)
                storePassword System.getenv("KEYSTORE_PASSWORD") ?: keystoreProperties['storePassword'] ?: project.findProperty("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS") ?: keystoreProperties['keyAlias'] ?: project.findProperty("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD") ?: keystoreProperties['keyPassword'] ?: project.findProperty("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePath) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            if (keystorePath) {
                signingConfig signingConfigs.debug
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeCompiler.get()
    }
}

dependencies {
    implementation libs.appCompat
    implementation platform(libs.composeBom)
    androidTestImplementation platform(libs.composeBom)
    implementation libs.androidxActivityCompose
    implementation libs.composeMaterial3
    implementation libs.composeUiToolingPreview
    implementation "androidx.compose.material:material-icons-extended"
    implementation 'io.coil-kt:coil-compose:2.6.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0'
    
    // Room
    implementation libs.room.runtime
    implementation libs.room.ktx
    ksp libs.room.compiler

    debugImplementation libs.composeUiTooling
    debugImplementation libs.composeUiTestManifest
}
